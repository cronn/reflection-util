buildscript {
    repositories {
        mavenCentral()
    }
    dependencyLocking {
        lockAllConfigurations()
    }
}

plugins {
    id 'maven-publish'
    id 'signing'
    id 'jacoco-report-aggregation'

    id 'org.sonarqube' version 'latest.release'
    id 'info.solidsoft.pitest' version 'latest.release'
}

group = 'de.cronn'

version = "2.13.2"

if (System.env.BUILD_NUMBER) {
    version = "${version}-SNAPSHOT-b${System.env.BUILD_NUMBER}"
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    dependencies {
        components.all { ComponentMetadataDetails details ->
            if (details.id.version =~ /(?i).+(-|\.)(CANDIDATE|RC|BETA|ALPHA|PR|M\d+).*/) {
                details.status = 'milestone'
            }
        }
    }

    dependencyLocking {
        lockAllConfigurations()
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
        dependsOn test
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = 'UTF-8'

pitest {
    timestampedReports = false
    outputFormats = ['XML', 'HTML']
}

wrapper {
    gradleVersion = "7.4"
    distributionType = Wrapper.DistributionType.ALL
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        jacocoAggregation project(':java-17-tests')
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            pom {
                name = project.name
                description = 'Java Reflection Utility Classes'
                url = 'https://github.com/cronn/reflection-util'

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        id = "benedikt.waldvogel"
                        name = "Benedikt Waldvogel"
                        email = "benedikt.waldvogel@cronn.de"
                    }
                    developer {
                        id = "mark.s.fischer"
                        name = "Mark S. Fischer"
                        email = "reflection-util.x.msf@spam-en.de"
                    }
                }

                scm {
                    url = "https://github.com/cronn/reflection-util"
                }
            }

            from components.java

            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.hasProperty('nexusUsername') ? project.property('nexusUsername') : System.getenv('NEXUS_USERNAME')
                password = project.hasProperty('nexusPassword') ? project.property('nexusPassword') : System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()

    maxHeapSize = "256m"

    try {
        if (Runtime.version().feature() >= 14) {
            jvmArgs += '-XX:+ShowCodeDetailsInExceptionMessages'
        }
    } catch (MissingMethodException ignore) {
        // Do not enable ShowCodeDetailsInExceptionMessages on older JVMs
    }
}

dependencies {
    implementation "net.bytebuddy:byte-buddy:latest.release"
    implementation "org.objenesis:objenesis:latest.release"

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'latest.release'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: 'latest.release'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'latest.release'
    testImplementation "org.mockito:mockito-core:latest.release"
    testImplementation "org.assertj:assertj-core:latest.release"
    testImplementation "javax.validation:validation-api:latest.release"
    testImplementation "org.apache.commons:commons-lang3:latest.release"
    testImplementation "cglib:cglib:latest.release"
    testImplementation "org.javassist:javassist:latest.release"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:latest.release"

    testImplementation "org.openjdk.jmh:jmh-core:latest.release"
    testRuntimeOnly "org.openjdk.jmh:jmh-generator-annprocess:latest.release"

    // no real transitive dependency but we use it to annotate method contracts to help the IDE understand the code
    compileOnly "org.jetbrains:annotations:latest.release"
    testCompileOnly "org.jetbrains:annotations:latest.release"
}
